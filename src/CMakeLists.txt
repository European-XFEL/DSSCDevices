add_library (
    ${CMAKE_PROJECT_NAME} SHARED
)

find_package(Threads REQUIRED)

target_sources(
    ${CMAKE_PROJECT_NAME}
    PRIVATE
    DsscPpt/DsscPpt.cc
    DsscPpt/DsscConfigHashWriter.cc
    DsscPpt/DsscPptAPI.cc

    LadderParameterTrimming/DsscKaraboRegisterConfig.cc
    LadderParameterTrimming/DsscLadderParameterTrimming.cc
    LadderParameterTrimming/DsscTrimPptAPI.cc
)


target_compile_options(
    ${CMAKE_PROJECT_NAME}
    PUBLIC -Wfatal-errors -Wno-unused-local-typedefs
           -Wno-deprecated-declarations -Wall
    -DF2IO  # DSSC Acquisition board version
    -DHAVE_HDF5  # Required for DSSC libraries
)


target_include_directories(
    ${CMAKE_PROJECT_NAME} SYSTEM
    PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}
    $ENV{KARABO}/extern/include/dssc
    $ENV{KARABO}/extern/include/dssc/chipinterface
    $ENV{KARABO}/extern/include/dssc/configreg
    $ENV{KARABO}/extern/include/dssc/dsschdf5
    $ENV{KARABO}/extern/include/dssc/ppt
    $ENV{KARABO}/extern/include/dssc/pptdatareceiver
    $ENV{KARABO}/extern/include/dssc/sequence
    $ENV{KARABO}/extern/include/dssc/utils
    $ENV{KARABO}/extern/include/hdf5
    $ENV{KARABO}/extern/dssc_minuit/include
)

link_directories($ENV{KARABO}/extern/lib/dssc)

target_link_libraries(
    ${CMAKE_PROJECT_NAME}
    PUBLIC
    Threads::Threads
    ${KARABO_LIB_TARGET_NAME}
    CHIPInterface
    ConfigReg
    DsscDependencies
    DsscHdf5
    PPTDataReceiver
    PPT
    Sequencer
    Utils
)


# Finds Git - it will be used by the custom command that generates version.hh
# A successful find_package for Git, sets the variable GIT_EXECUTABLE with the
# absolute path to the Git CLI on the local system.
find_package(Git REQUIRED)

# Generates the version.hh for the device whenever there is an update to the
# .git directory that might have been caused by a device version change and that
# update is newer than the current version.hh file.
set(GIT_REPO_PRIV_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../.git)
set(VERSION_HEADER ${CMAKE_CURRENT_SOURCE_DIR}/version.hh)
string(TOUPPER ${CMAKE_PROJECT_NAME} PROJECT_NAME_UPPER)

add_custom_command(
    OUTPUT ${VERSION_HEADER}
    COMMAND echo "// WARNING: This file is auto generated by the Makefile." > ${VERSION_HEADER}
    COMMAND echo "#ifndef PACKAGE_VERSION" >> ${VERSION_HEADER}
    COMMAND echo -n "#define PACKAGE_VERSION \"${CMAKE_PROJECT_NAME}-" >> ${VERSION_HEADER}
    COMMAND ${GIT_EXECUTABLE} describe --tags --match "*.*.*" --dirty --always | tr -d "\\n" >> ${VERSION_HEADER}
    COMMAND echo "\"" >> ${VERSION_HEADER}
    COMMAND echo "#endif" >> ${VERSION_HEADER}
    VERBATIM
    DEPENDS ${GIT_REPO_PRIV_DIR}/HEAD ${GIT_REPO_PRIV_DIR}/refs/tags
)

add_custom_target(
    UpdateVersionHeader ALL
    DEPENDS ${VERSION_HEADER}
)

add_dependencies(${CMAKE_PROJECT_NAME} UpdateVersionHeader)


if (BUILD_TESTS)

    enable_testing()

    add_executable(
       test-${CMAKE_PROJECT_NAME}
       tests/testrunner.cc   # The test runner entry point
       tests/testDsscPpt.cc
       tests/testLadderParameterTrimming.cc

    )

    include("../cmake/find_dep.cmake")
    find_dep(gtest gtest)

    target_compile_options(
        test-${CMAKE_PROJECT_NAME}
        PUBLIC -Wfatal-errors -Wno-unused-local-typedefs
               -Wno-deprecated-declarations -Wall)

    target_link_libraries(
        test-${CMAKE_PROJECT_NAME}
        PRIVATE
        Threads::Threads
        ${CMAKE_PROJECT_NAME}
        ${gtest_LIB}
    )

    add_test(NAME ${CMAKE_PROJECT_NAME}Tests COMMAND test-${CMAKE_PROJECT_NAME})

endif()
